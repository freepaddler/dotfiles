# vim: ft=zsh

export _ENV_SHELL="zsh"
source "$_ENV_CONFIG_DIR/_env_functions"
_env_debug "zshrc"

_ENV_ZSH_DIR="$_ENV_CONFIG_DIR/zsh"
_env_clean_add _ENV_ZSH_DIR

## options
# word splitting (POSIX)
setopt sh_word_split
# case-unsensitive-globbing
setopt no_case_glob
# cd if cd is missed
setopt auto_cd
setopt cd_silent
# auto corrections
setopt correct
#setopt correct_all
# protect > overwrites
setopt no_clobber
setopt append_create


## key bindings
bindkey -e
# delete
bindkey          '^[[3~' delete-char
# home, end
bindkey          '^[[1~' beginning-of-line
bindkey          '^[[4~' end-of-line
# pgup, pgdown
bindkey          '^[[5~' history-search-backward
bindkey          '^[[6~' history-search-forward
# ctrl-left, ctrl-right
bindkey          '^[[1;5C' forward-word
bindkey          '^[[1;5D' backward-word
# FreeBSD console ctrl-left, ctrl-right
bindkey          '^[[5C' forward-word
bindkey          '^[[5D' backward-word
# shift-left, shift-right
bindkey          '^[[1;2D' backward-word
bindkey          '^[[1;2C' forward-word

# common
bindkey          '^n' history-beginning-search-forward
bindkey          '^p' history-beginning-search-backward
# vi-mode
bindkey -M viins '^n' history-beginning-search-forward
bindkey -M viins '^p' history-beginning-search-backward
bindkey -M viins '^[.' insert-last-word

# use Ctrl-f to trigger fzf completion
ZVM_INIT_MODE=sourcing
fzf_tab() { zle -U "$FZF_COMPLETION_TRIGGER"$'\t'; }
zle -N fzf_tab
bindkey -M viins '^G' fzf_tab
bindkey -M emacs '^G' fzf_tab

# tmux-sessionizer
tmux_sessionizer_widget() {
  if [[ -n "$TMUX" ]]; then
    BUFFER='tmux display-popup -E tmux-sessionizer'
  else
    BUFFER='tmux-sessionizer'
  fi
  zle accept-line
}
zle -N tmux_sessionizer_widget

# bind it (pick your key)
bindkey '^F' tmux_sessionizer_widget

### history
alias h='history -i -100'
## file
HISTSIZE=1000
SAVEHIST=10000
setopt extended_history
setopt append_history
setopt inc_append_history_time
# don't save dups to file
setopt hist_save_no_dups
# don't save history command itself
setopt hist_no_store
## session
# omit commands started with space
setopt hist_ignore_space
# ingnore dups one after another
setopt hist_ignore_dups
# ignore dups in session history
#setopt hist_ignore_all_dups
# don't search dups
setopt hist_find_no_dups
# show command from history instead running !!
setopt hist_verify


## prompt
PROMPT='%B%F{green}%n%b'
if [[ -n "$SSH_CONNECTION" ]]; then
    PROMPT+='%f@%F{yellow}%m'
fi
PROMPT+=$' %F{cyan}%~ \n%F{green}❯%f '
PS2='%F{8}↳%f '


## init plugin manager
_env_source "$_ENV_ZSH_DIR/zinit.zsh" 


## completions
# apply LS_COLORS
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
# simple menu
zstyle ':completion:*' menu select=2
setopt automenu
# group results by type (files, dirs, etc.)
zstyle ':completion:*' group-name ''
# display descriptions like “(directory)” inline
zstyle ':completion:*:descriptions' format '%F{8}↳ %d %f'

# make fpath unique (no duplicates) and global
typeset -gU fpath
# prepend existing dirs
fpath=(
  ${_ENV_ZSH_DIR}/completions(N-/)
  ${HOMEBREW_PREFIX:+${HOMEBREW_PREFIX}/share/zsh/site-functions(N-/)}
  ${HOMEBREW_PREFIX:+${HOMEBREW_PREFIX}/share/zsh-completions(N-/)}
  $fpath
)

# add zsh-completions
zinit ice depth"1"
zinit light zsh-users/zsh-completions

# initialize
autoload -Uz compinit
autoload -Uz run-help
alias help="run-help"
compinit -u
# block later re-init
compinit() { return 0 }
# bash completions
autoload -Uz bashcompinit
bashcompinit -u
# block later re-init
bashcompinit() { return 0 }


## common
_env_source "$_ENV_CONFIG_DIR/functions"
_env_source "$_ENV_CONFIG_DIR/aliases"
_env_source "$_ENV_CONFIG_DIR/${_ENV_OS}_rc"
_env_source "$_ENV_CONFIG_DIR/rc"/*(.N)


## functions


## other plugins
# pure prompt
zinit ice depth"1" pick"async.zsh" src"pure.zsh"
zinit light sindresorhus/pure
source "$_ENV_ZSH_DIR/pure.zsh"

# fzf-tab
zinit ice depth"1"
zinit light Aloxaf/fzf-tab
source "$_ENV_ZSH_DIR/fzf-tab.zsh"

# syntax highlight
zinit ice depth"1"
zinit light zsh-users/zsh-syntax-highlighting

## bottom
_env_clean
return 0
