# vim: ft=zsh

export _ENV_SHELL="zsh"
source "$_ENV_CONFIG_DIR/_env_functions"
_env_debug "zshrc"

_ENV_ZSH_DIR="$_ENV_CONFIG_DIR/zsh"
_env_clean_add _ENV_ZSH_DIR

## options
# word splitting (POSIX)
setopt sh_word_split
# case-unsensitive-globbing
setopt no_case_glob
# cd if cd is missed
setopt auto_cd
setopt cd_silent
# auto corrections
setopt correct
#setopt correct_all
# protect > overwrites
setopt no_clobber
setopt append_create


## key bindings
# emacs key binding
bindkey -e
# home, end
bindkey '^[[H' beginning-of-line
bindkey '^[[F' end-of-line
# pgup, pgdown
bindkey '^[[5~' history-search-backward
bindkey '^[[6~' history-search-forward
# delete, insert
bindkey '^[[3~' delete-char
bindkey '^[[2~' quoted-insert


### history
alias h='history -i -100'
## file
HISTSIZE=1000
SAVEHIST=10000
setopt extended_history
setopt append_history
setopt inc_append_history_time
# don't save dups to file
setopt hist_save_no_dups
# don't save history command itself
setopt hist_no_store
## session
# omit commands started with space
setopt hist_ignore_space
# ingnore dups one after another
setopt hist_ignore_dups
# ignore dups in session history
#setopt hist_ignore_all_dups
# don't search dups
setopt hist_find_no_dups
# show command from history instead running !!
setopt hist_verify


## completions
# apply LS_COLORS
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
# cycle through menu on multiple matches with incremental search
#zstyle ':completion:*' menu select search
#zstyle ':completion:*:*:*:default' menu select search
# simple menu
zstyle ':completion:*' menu select=2
setopt automenu
# group results by type (files, dirs, etc.)
zstyle ':completion:*' group-name ''
# display descriptions like “(directory)” inline
zstyle ':completion:*:descriptions' format '%F{8}↳ %d %f'

# make fpath unique (no duplicates) and global
typeset -gU fpath
# prepend existing dirs
fpath=(
  ${_ENV_ZSH_DIR}/completions(N-/)
  ${HOMEBREW_PREFIX:+${HOMEBREW_PREFIX}/share/zsh/site-functions(N-/)}
  ${HOMEBREW_PREFIX:+${HOMEBREW_PREFIX}/share/zsh-completions(N-/)}
  $fpath
)

## init
# completions
autoload -Uz compinit
autoload -Uz run-help
alias help="run-help"
compinit -u
# block later re-init
compinit() { return 0 }
# bash completions
autoload -Uz bashcompinit
bashcompinit -u
# block later re-init
bashcompinit() { return 0 }

## prompt
if [[ -f "$_ENV_ZSH_DIR/prompt.zsh" ]]; then
    source "$_ENV_ZSH_DIR/prompt.zsh"
else
    PROMPT='%B%F{green}%n%b'
    if [[ -n "$SSH_CONNECTION" ]]; then
        PROMPT+='%f@%F{yellow}%m'
    fi
    PROMPT+=$' %F{cyan}%~ \n%F{green}❯%f '
    PS2='%F{8}↳%f '
fi


## plugins
# autosuggestions
_env_source "$HOMEBREW_PREFIX"/share/zsh-autosuggestions/zsh-autosuggestions.zsh
# syntax highlight
_env_source "$HOMEBREW_PREFIX"/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh


## cutomized rc
_env_source "$_ENV_CONFIG_DIR/${_ENV_OS}_rc"
_env_source "$_ENV_CONFIG_DIR/rc"/*(.N)


## functions

## bottom
_env_clean
return 0
